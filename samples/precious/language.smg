; $Id: lang.txt,v 1.2 1997/04/25 01:13:45 oliver Exp $
; Lang.Txt for Precious Days
; ~~~~~~~~~~~~~~~~~~~~~~~~~~
; Copyright (c) 1991 by Oliver Smith / KingFisher Software (oliver@kfs.org)
;
; This software is furnished on the understanding that it may be used and or
; copied  only  with the inclusion of this notice.  No title or ownership of
; this  software is hearby transferred.  The information in this software is
; subject  to  change  without notice.  No responsibility is assumed for the
; use or reliability of this software.


; Verbs which need adjustments (which can't be made till future updates of
; AMUL itself):
;	send <player> sleep, where (needs where <player>)

;; Features to be added in future versions (* indicates needs AMUL 0.9.07)
;;
;;	Ability to climb down things like cliff face on a rope		*
;;	Mobiles able to pick things up and wander about with them	*
;;	!depart and !arrive verbs to control movement beyond !travel	*
;;	"SAVE" will (optionally) store players location			*
;;	Ability to take single-turn combat movements, e.g. "Hit" etc
;;	- without affecting current combat status			*
;;	A fortress north of the huge cliff/mine with more new problems
;;	More rooms
;;	More objects
;;	More tasks
;;	More mobiles
;;	a "secret" way to stop fights					*
;;	dice / roll
;;	correct handling of ?<noun> (i.e. value of object)
;;
;;	PLEASE send me YOUR (and your users) suggestions, and also any
;;	problems you have come across!
;;	Or contact the AMUL support guys:
;;		bpratt@splat.aarnet.edu.au
;;		boutwell@beta.tricity.wsu.edu

; --------------------------- Special Verbs! ------------------------------- ;

verb=!travel
syntax=any
	infl me cripple error Crippled		; Can't move if crippled.
	fighting me error "You can't leave in the middle of a fight! Use FLEE if you want to leave..."
	sitting error StandUp,lying error StandUp
;	nearto belowcliff dec seebelow

; Executed AFTER arriving in room
;!arrive				; Not implemented in AMUL 0.9.06
;syntax=any
;	nearto belowcliff inc seebelow
;	!nearto marshgas skip 2
;		gota brand 0 print "Walking into a marsh with a naked flame, very clever."
;		& blast marshgas MGas1 MGas2

!speech
syntax=any
	infl me dumb respond "You can't speak a word, you're dumbed!"
syntax=player=me text,respond talkself
syntax=text player,syntax noun2 noun1
syntax=player text,tell player '@xy @vbs you "@n2"',respond 'You @vb @n1 "@n2"'
syntax=text,announce others '@xx @vbs "@n1"',respond 'OK, @me @vbs "@n1"'
syntax=none error "You have to tell me what or who to @vb, softy."

; ---------------------------- Travel Verbs -------------------------------- ;

travel=north south east west northeast southeast southwest northwest up down in out

left travel,syntax=any,treatas west

right travel,syntax=any,treatas east

jump travel
syntax=noun=cow,sub 15 points me,respond "AAAGGHHH! Take that for being a blithering idiot!"
syntax=noun,respond HeyDiddle
syntax=player,respond JumpPlayer

verb=cross
syntax=none,!nearto bridge error "Yes, very.",else> travel
syntax=noun=bridge,checknear bridge,travel
syntax=noun,error CantVBThat

; -------------------------- Assorted Daemons ----------------------------- ;

verb=.downshute
syntax=any
	!inroom shute end
	tell me "Without warning the shute ends and you land, with a bump, on solid ground..."
	move botshute
	announce cansee "There is a muffled scream, a whoosh of air and suddenly, @me shoots out of the tunnel and lands with a bump."
	announce notsee "You hear a muffled scream from the shute, followed by a heavy thud nearby."

verb=.bumpshute
syntax=any
	!inroom shute end
	tell me "You suddenly go over a bump in the tunnel and begin to go even faster!"
	announceto tvroom "There is a dull THUD which seems to come from inside the chimney breast..."
	start .downshute `10

verb=.cons,syntax=any
	respond "Actually, I was kidding - I don't really DETEST you!"

verb=.deepsleep	; prevent players waking immediately.
syntax=any
	! infl me sleep end
	!inroom copse1 respond "ZZZzzz..."
	print Refreshed,cure me sleep,action cansee $50

verb=.recover2
syntax=any
	!infl me sleep end
	full stam me print Refreshed,& cure me sleep,&> action cansee $50
	add 2 stam me,start .recover2 5

verb=.recover
syntax=any
	!infl me sleep end
	full stam me print Refreshed,& cure me sleep,&> action cansee $50
	add 1 stam me,start .recover 8

verb=.sleep
syntax=any
	infl me sleep end,active .deepsleep cancel .deepsleep
	start .deepsleep `10,inflict me sleep
	full stamina me skip 2
		nearto cosy start .recover2 16,else start .recover 20
	action cansee GoneSleep

verb=.combat
syntax=any
	!fighting me end
	interact enemy,fighting me combat
	fighting me start .combat 5

verb=.burn
syntax=noun noun
	lit noun objaction noun "The @n1 burns away to nothing...",& destroy noun
	end

verb=.burnout
syntax=noun noun
	lit noun objaction noun "The @n1 has gone out.",else end
	fire noun douse noun,else state noun 1

verb=.gcollapse
syntax=any
	! lit gates end
	state gates 2,blast gates Gates1 Gates2,douse gates

verb=.gburn
syntax=any
	! lit gates end
	objannounce gates "The gates creak and groan, uneasilly..."
	gstart .gcollapse 38

verb=.cantpee
syntax=any
	print "You feel a tingling sensation in your abdomen."

verb=.drown
syntax=any
	!inroom underlake end
	print "Suddenly you can hold your breath no-longer, and sadly, you drown."
	killme

verb=.pushed,syntax=any,end		; this is a dummy daemon

verb=.saved
syntax=any
	cancel .pushed
	print "??? HUH ???", add 42 points me
	inflict me sleep, start .deepsleep 5,move home,action cansee "The Holy One has rescued @me from near death."

verb=.fallcliff7
syntax=any
	active .pushed do .saved,& end
	rand 500 >490 do .saved,& end
	print "> SPLAT <"
	announce global "Somewhere in the distance you hear a faint but ungodly sound. You feel a moments burst of uncanny remorse, and then it passes."
	killme

verb=.fallcliff6,syntax=any,print "1..", start .fallcliff7 1

verb=.fallcliff5,syntax=any,print "2..", start .fallcliff6 1

verb=.fallcliff4,syntax=any,print "3..", start .fallcliff5 1

verb=.fallcliff3
syntax=any
	infl me sleep then cure me sleep,& print "Wake wakey ... {"
	print "4 seconds to impact!"
	start .fallcliff4 1

verb=.fallcliff2
syntax=any
	if infl me sleep then cure me sleep
	print "Oh, did I mention? There's rocks directly below!"
	announce global "In the distance you hear another, more terrifying scream..."
	print "You pass out ... {",inflict me sleep,start .deepsleep 10
	action cansee "@me has passed out. Poor thing."
	start .fallcliff3 20

verb=.fallcliff1
syntax=any
	infl me sleep cancel .deepsleep, & cure me sleep, & print wokenfall, & look
	announce global "In the distance you hear a long, echoing, blood-curdling scream..."
	start .fallcliff2 `10

verb=.gotpushed
syntax=any
	!inroom gcliff end
	gota umbrella 0 respond "You almost topple over the edge of the cliff, but thanks to your brolly you just manage to keep your balance."
	start .pushed 60,action cansee "@xx has just fallen over the edge of the cliff."
	move fallcliff, start .fallcliff1 `5
	print "Suddenly your balance goes completely and you topple and fall over the edge of the cliff..."

; ------------------------- More Special Verbs ----------------------------- ;

!magsleep
syntax=any
	active .deepsleep cancel .deepsleep
	start .deepsleep `10,inflict me sleep,action cansee GoneSleep
	error "Your spell has failed and the power of the magic puts you to sleep..."

!powsleep
syntax=any
	active .deepsleep cancel .deepsleep
	start .deepsleep `21,inflict me sleep,action cansee Vanishes
	print PassOut

!zonksleep	; Like powsleep but quiet.
syntax=any
	active .deepsleep cancel .deepsleep
	start .deepsleep `22,inflict me sleep,print PassOut

!passout
syntax=any
	active .deepsleep cancel .deepsleep
	start .deepsleep `20,inflict me sleep,action cansee "@me has passed out."

!rank11,syntax=any,toprank end,myrank <11 error $74,rand 100 <15 error $26

!rank10,syntax=any,myrank <10 error $74,myrank >10 do !rank11,& end
	rand 100 <20 error $26

!rank9,syntax=any,myrank <9 error $74,myrank >9 do !rank10,& end
	rand 100 <25 error $26

!rank8,syntax=any,myrank <8 error $74,myrank >8 do !rank9,& end
	rand 100 <30 do !magsleep

!rank7,syntax=any,myrank <7 error $74,myrank >7 do !rank8,& end
	rand 100 <35 do !magsleep

!rank6,syntax=any,myrank <6 error $74,myrank >6 do !rank7,& end
	rand 100 <45 do !magsleep

!rank5,syntax=any,myrank <5 error $74,myrank >5 do !rank6,& end
	rand 100 <55 do !magsleep

!rank4,syntax=any,myrank <4 error $74,myrank >4 do !rank5,& end
	rand 100 <65 do !magsleep

!rank3,syntax=any,myrank <3 error $74,myrank >3 do !rank4,& end
	rand 100 <75 do !magsleep

!rank2,syntax=any,myrank <2 error $74,myrank >2 do !rank3,& end
	rand 100 <85 do !magsleep

!pre-magic
syntax=noun,error "The spell only works on people!"
syntax=player=me,syntax none none
syntax=player,stat magic me <1 error "You don't have enough magic points to perform the spell!"

!get			; pick something up...
syntax=noun noun,	get noun,action others PickedUp,respond GotIt
syntax=noun,		get noun,action others PickedUp,respond GotIt

!burnme
syntax=noun,lit noun1 print OUCH,& sub 2 stam me,& fail
syntax=noun noun,lit noun1 print OUCH,& sub 2 stam me,& fail

!needtwo
syntax=any
	toprank respond "You easily manage to @vb the @n1."
	!gothelp error "It's too much for you; you'll have to get someone to help you..."
	!cansee helper error "You'll need someone to help you with it!"
	!visibleto helper error "@he can't see you to help you!"
	gothelp interact helper
	& print "Together with @he you manage to @vb the @n1."
	& tell helper "Together with @xx you manage to @vb the @n1."

!teddybear
syntax=noun
	state noun 1 destroy bear, & setpre me "teddy murdering", & print "The teddy gives off a tiny, heartbreaking whimper and disintigrates!", & sub 15 points me, &> destroy bear
	state noun 1, setpre me "teddy bashing", print "You manage to mutilate the teddy, and a from inside it a huge diamond drops out onto the floor!"
	recover diamond,send diamond here,state diamond 0,action others "@xx has dropped a huge diamond nearby!",sub 10 points me

verb=.restcirc				; restore the circle
syntax=noun
	recover circle,send circle tavpse,state circle 1
	objaction circle "Suddenly, out of nowhere, a circle appears in the middle of the floor."

verb=.endcirc				; Circle magic expire
syntax=noun
	inc circle objaction circle CircStop

!touchcirc
syntax=noun
	gstart .restcirc 60,destroy circle
	action others "The circle has vanished."
	respond "When you touch the circle, it suddenly vanishes."

verb=nod
syntax=none,action cansee UEmote,respond IEmote
syntax=text,action cansee IEmoteX,respond IEmoteX

verb=laugh
syntax=none,announce others UEmote,respond IEmote
syntax=text,announce others UEmoteX,respond IEmoteX

; ---------------------- Basic Adventure Commands ------------------------- ;

verb=quit dream	; exit the game		(dream = can be done whilst asleep)
syntax=any
	inroom limbo respond "Limbo is nowhere, so there is nowhere to go when you leave the game, which means you're already there."
	fighting me error "You can't quit in the middle of a fight, dumbo! You have to flee first!"
	quit

verb=look
syntax=any
	infl me blind error YourBlind
	! light respond "It's too dark to see anything here!"
syntax=none,-> look
syntax=noun=wall
	inroom tvroom respond ExamWPaper,inroom tavclos respond ExamClosW
	inroom store respond NoWallEre,inroom limbo respond NoWallEre
	inroom heaven respond NoWallEre
	inroom slopetun3 respond "When you look carefully, the crack in the eastern wall seems to lead into another tunnel!"
syntax=noun
	noun1 all error NoAllYet
	noun1 breeze respond CantExam
	checknear noun,do !burnme
syntax=noun=door,nearto DoorPort respond ExamPDoor
syntax=noun=window,inroom cpass4e1 respond ExamW1,inroom tavs1 respond ExamW2
syntax=noun=paper
	!carrying noun respond "The writing is too small; you'll have to pick it up first."
	print ReadPaper,destroy noun,-> add 6 points me
syntax=noun=portrait
	ishere ghost respond "Uncanny! The bloke in the picture looks just like the ghost!"
	respond "It's a very good picture, just a shame the subject looks such a miserable geek."
syntax=noun=loo
	state noun 0 respond "It's full of foul smelling yellow liquid that you wouldn't want to drink."
	respond "Someone had the courtesy to flush it after it was last used."
syntax=noun
	noun1 doorway respond ExamDWAY
	noun1 forcefield respond ExamFField
	noun1 stairs respond ExamStairs
	noun1 wallpaper respond ExamWPaper
	noun1 cup respond ExamCup
	noun1 brand respond ExamTorch
	noun1 bone respond ExamBone
	noun1 sign respond ExamSign
	noun1 shelves respond ExamShelf
	noun1 graffiti respond ExamGraf
	noun1 notices respond notices
	noun1 footprints respond ExamFPrts
	noun1 pan respond ExamPan
	noun1 wagon respond ExamWag
	noun1 view respond ExamW2
	respond "It looks just the same as it did last time you looked at it."

verb=who
syntax=any
	!inroom limbo who v,& end
	onlyuser respond "'Fraid you're on your own, bud."
	respond "You're in Limbo, so you can't tell who is or isn't around."

verb=qw,syntax=any,inroom limbo treatas who,print "Current users: {",who b

verb=save dream,syntax=none,save

verb=score dream,syntax=none,score v

verb=qs dream,syntax=none,score b

verb=examine,syntax=noun,treatas look

verb=rank,syntax=none,print "You are @me the @mr."

verb=exits,syntax=any,infl me blind respond YourBlind,print "Exits:",exits

verb=autoexits	dream
syntax=none
	autoexits respond "Auto exits are already on!"
	- autoexits on,print "Each time you move from one room to another, the exits will be listed. Use UNAUTO to disable this."

verb=unauto	dream
syntax=none
	!autoexits respond "Auto exits aren't on anyway!"
	- autoexits off,print "'Auto Exits' is now off."

verb=inventory
syntax=any
	infl me blind respond "Your blind - so you can't tell what you're carrying!"
	invent

verb=brief dream
syntax=any
	rdmode b respond ModeFail
	rdmode b,print "You will be given brief descriptions of each room now; use Look to see the long description of a room."

verb=verbose dream
syntax=any
	rdmode v respond ModeFail
	rdmode v,print "You will be given long desrciptions of each room now."

verb=roomcount dream
syntax=any
	rdmode r respond ModeFail
	rdmode r,print "The long description of each room will only be given the first time you visit a room, to see the long description again use Look."

verb=value
syntax=noun
	noun1 all error NoAllYet,checknear noun,print "@n1 currently worth: @v1 point(s)."

verb=weigh
syntax=noun
	noun1 all error NoAllYet,checknear noun,print "@n1 currently weighs around @w1g."

; ---------------------------- Main Commands ------------------------------ ;

verb=get ?hcae		; Look for objects HERE before CARRIED
syntax=verb verb=up noun,syntax noun none
syntax=verb noun verb=up,syntax noun none
syntax=none,error verbWhat
syntax=player,error DontBeDaft
syntax=noun noun
	checknear noun2,checknear noun1
	error "Sorry - container routines aren't finished yet!"
syntax=noun
	noun1 well respond "Thanks, but I'm fine."
	noun1 lost print "OK...",&> killme
	noun1 all error NoAllYet,checknear noun
	noun1 circle do !touchcirc,& end
	noun1 stove error MoveStove,noun1 phone error MovePhone
	noun1 mud error "It's VERY goey and simply )GLOOP(s out of your hand."
	checkget noun,!noun1 brand do !burnme,do !get

; - - - - - - - - - -

!drop
syntax=noun
	drop noun,action others "@xx has dropped the @n1."
	inroom spring1 end
syntax=noun=vase,destroy vase,action others "The @n1 smashes.",respond "The vase smashes when it hits the floor. Clumsy!"
syntax=noun=paper,destroy paper,action others "The @n1 disintegrates.",respond "It was REALLY fragile, and it disintegrates."

verb=drop
syntax=none,error verbWhat
syntax=player,error DontBeDaft
syntax=noun noun	; Want to use a container!
	checknear noun2,checknear noun1
	noun2 spring syntax noun none	; Allow drop X in spring.
	noun2 river send noun1 store,& respond "That's the last you'll see of that!"
	!noun2 circle error "Sorry - container routines aren't finished yet!"
	send noun1 store,!dec noun2 respond Nothing
	objaction circle "The circle has started glowing.",gstart .endcirc `20
	end
syntax=noun
	noun1 all error NoAllYet
	!carrying noun error NotGotNoun
	print "@n1 dropped.",do !drop
	!inroom spring1 end,!noun1 crown end,!state noun 1 end
	settask 4,-> showtasks

verb=!put
syntax=noun
	!carrying noun error NotGotNoun
	print "You carefully put the @n1 down."
	drop noun,action others "@xx has put the @n1 on the ground."

verb=put
syntax=noun verb=down,syntax noun1 none
syntax=noun,noun1 paper do !put,& end,noun1 vase do !put,& end
syntax=any,treatas drop

; - - - - - - - - - -

!feedcat
syntax=noun,syntax dish cat
syntax=noun noun
	!state noun1 1 error "The cat seems a little dissapointed at the dishes lack of content."
	inc noun1 print "The cat is delighted with the milk, and laps it up ravenously. And when it has finished you are astonished to find that the dish has turned into solid gold."
	action others "@xx offers the cat a bowl full of milk, which it ravenously downs."
	add 25 points me,taskdone 1 end,settask 1,showtasks


verb=give
syntax=player noun
	noun2 all error NoAllYet
	carrying noun skip 2
	 nearto noun error "You'll have to pick it up first."
	 error notgotnoun
	!sameroom player error PlyrNotHere
	!cangive noun player print "@n1 can't carry any more! Dropped."
	&> do !drop
	interact player,give noun player,tell player "@xx has given you the @n2."
	action cansee "@xx gives the @n2 to @n1."
	respond "You give the @n2 to @n1."
syntax=noun=cat noun=dish,syntax noun2 noun1
syntax=noun=book noun=ghost,syntax noun2 noun1
syntax=noun=dog noun=bone
	!nearto noun1 respond "Very thoughtful of you, but he's not here to take advantage of your generocity."
	!carrying noun2 respond "That's a fine idea, but you don't have one to give him."
	syntax noun2 noun1
syntax=noun,!carrying noun respond NotGotNoun,error "Who to?"
syntax=noun noun,checknear noun2,checknear noun1
syntax=noun=bone noun=dog,respond DogNoBone
syntax=noun=dish noun=cat,do !feedcat
syntax=noun=ghost noun=book,respond "Despite your attempts, the ghost is totally unable to take hold of the book. Desperately, it motions you to read the book."
syntax=noun player,syntax player noun
syntax=player=me noun
	carrying noun error "You already got the @n1."
	checknear noun,error "Pick it up yourself."
syntax=player,error VerbWhat

; - - - - - - - - - -

verb=open	1CHAE
syntax=noun=door
	inroom kitchen syntax stove none
	inroom cpass4e1 error DoorPic,inroom sqroom error DoorPic
syntax=noun,checknear noun,do !burnme
syntax=noun=portcullis
	state noun 1 respond "It's an ancient form of security door, so it doesn't open that easily!"
	respond "It already IS raised!"
syntax=noun=window
	inroom cpass4e1 error OnlyPaint,inroom tavs1 error NoOCWin
syntax=noun
	checknear noun,do !burnme
	noun1 bear do !teddybear,& end
	!opens noun error CantVbThat
	state noun 2 error "The @n1 is locked shut, you'll need to unlock it first."
	state noun 0 error AlreadyDone
	noun1 gates do !needtwo
	print ItsNow,action cansee IDidItE,actionfrom noun HeDidItE
	-> state noun 0
syntax=noun noun
	checknear noun2,checknear noun1
	!carrying noun2 error NotGotTool
	!opens noun1 error CantVbThat
	state noun1 0 error AlreadyDone
	error UseHands

; - - - - - - - - - -

verb=close	0CHAE
syntax=noun=door
	inroom kitchen syntax stove none
	inroom patio error NoClPatio,inroom dining error NoClPatio
	inroom sqroom error DoorPic,inroom cpass4e1 error DoorPic
syntax=noun,checknear noun,do !burnme
syntax=noun=portcullis
	state noun 1 respond "It's already been lowered!"
	stat str me <55 respond "You aren't strong enough to lower it!"
	inc portcullis actionfrom portcullis "@xx has lowered the portcullis."
	respond "You have lowered the portcullis."
syntax=noun=window
	inroom cpass4e1 error OnlyPaint,inroom tavs1 error NoOCWin
syntax=noun
	!opens noun error "It's not open, so how can you close it?"
	!state noun 0 error AlreadyDone
	print ItsNow,action cansee IDidIt,actionfrom noun HeDidIt
	-> state noun 1
syntax=noun noun
	checknear noun2,checknear noun1,!carrying noun2 error NotGotTool
	!opens noun1 error CantVbThat,!state noun1 0 error AlreadyDone
	error UseHands

; - - - - - - - - - -

verb=douse	0chae
syntax=noun
	checknear noun
	noun1 stove error MagicFlames
	noun1 fire error MagicFlames
syntax=noun
	!burns noun respond notlit
	!lit noun respond notlit
syntax=noun=gates
	active .gcollapse error "The flames are too fierce and try as you might, you cannot put them out."
syntax=noun
	print "The @n1 is no-longer aflame."
	action cansee IDidIt,actionfrom noun HeDidIt
	fire noun douse noun,else state noun 1

; - - - - - - - - - -

verb=light	1chae 0chae
syntax=noun
	checknear noun
	nearto stove syntax noun stove,nearto fire syntax noun fire
	gota brand 0 syntax noun brand,carrying wand syntax noun wand
	noun1 brand error "You need something to light it with, plonker."
	error "From what?"
syntax=noun noun
	checknear noun,checknear noun2
	lit noun1 respond AlreadyDone
	!noun2 wand skip 2
		!carrying noun2 error NotGotTool
		!state noun2 0 error "The wand lets out a tiny PHHUTTT! and does nothing else."
	noun2 wand skip 1
		!lit noun2 error "The @n2 isn't burning, so you can't light anything else from it, dunderhead!"
	!noun1 portcullis skip 2
		state noun 1 respond 'Written on the bars is "Guaranteed Fire Resistant", and would you believe it, it was quite right!"
		respond "It's been raised, so you can't find anything to set fire to, and anyway, they're usually made of metal, you know."
	!burns noun1 respond CantVbThat
	!noun1 violin skip 1,state noun1 2 error "You can't, it's made of GOLD - it doesn't burn!"
	state noun 2 respond "It refuses to catch light."
	noun2 wand print WandLight,& do !powsleep
	else print "You light the @n1."
	fire noun burn noun,else state noun 0
	!carrying noun1 action others "@xx has set the @n1 alight."
	actionfrom noun "@xx has lit the @n1."
	noun1 gates gstart .gburn `26,& end
	noun1 brand gstart .burn `1000,& end
	noun1 branch gstart .burn 16,& end
	gstart .burn `30

verb=burn
syntax=none,respond "I hope YOU go to hell, too, scum!"
syntax=any,treatas light

; --------------------------- Misc. Commands ------------------------------ ;

verb=bug
syntax=text,log "@me: (@mr/@rm) @n1",respond bugsug
syntax=any,error UseBugSug

verb=suggest
syntax=text,log "@me: (@mr/@rm) @n1",respond bugsug
syntax=any,error UseBugSug

verb=tie
syntax=noun
	checknear noun,do !burnme
	!carrying noun respond "Tying things is fiddly work; you'll have to pick the @n1 up first."
syntax=noun=rope
	!inc noun error AlreadyDone,myrank 1 add 2 points me
	respond "You fiddle for a while and eventually fasten a nice, neat knot into it."
syntax=noun
	noun1 tie respond "You fiddle about with it, but don't quite seem to have the knack of tying the thing, so you give it up as a bad job."
	error CantVbThat

verb=untie
syntax=noun
	checknear noun
	!carrying noun respond "Untying things is fiddly work; you'll have to pick the @n1 up first."
syntax=noun=rope
	do !burnme
	!dec noun error "Hmmm... I KNEW that someday someone would try to untie a STRAIGHT piece of string, dimwit."
	respond "It's a tricky one, but you eventually get the knot undone."
syntax=noun
	noun1 tie respond "It's not been tied!"
	error CantVbThat

verb=what,syntax=any,what

verb=locname,syntax=any,whereami

verb=help
syntax=player=me,error "That's NOT what I'm here for!"
syntax=player
	helping player respond "You are already helping @n1!"
	givinghelp print "You are no-longer helping @fr."
	else print "You are now helping @n1."
	help player,tell player "@me is now helping you."

; --------------------------- Magic Commands ------------------------------ ;

verb=summon
syntax=verb none,respond "Summon WHO?"
syntax=player=me,error "You'll have to ASK someone else to do that!"
syntax=noun
	carrying noun	respond "You've already got one! Imbercile!"
	nearto noun	error "It's already here, plonker!"
	error "Go fetch it yourself! Lazy bones!"
syntax=player
	inroom shute respond "You are moving too fast to perform the spell!"
	cansee player	respond "@n1 is already here!"
	action cansee MakeMag,do !rank6
	tell player "The scene around you begins to fade... {"
	summon player,respond success

; - - - - -

verb=change
syntax=player
	action cansee MakeMag
	!carrying cloak do !rank4,else print CloakShim
	print success,-> changesex player
syntax=noun,respond "No, I like it the way it is."
syntax=any, error "Try change <player> - to change <player>s sex."

; - - - - -

verb=.msleep
syntax=none
	infl me sleep end,print "A powerful magic overcomes you, and you fall asleep."
	do .sleep

verb=sleep
syntax=noun=bed
	checknear noun,state noun 0 respond "It's too uncomfortable to sleep on, so you get off again."
	state noun 0,respond Uncomfy1
syntax=any,do !pre-magic
syntax=player
	action cansee MakeMag,do !rank6,print success,senddaemon player .msleep 0
syntax=none
	infl me sleep respond "You already ARE asleep!"
	fighting me respond "Pathetic. Here you are, fending for your life, and you want to snooze?^MFORGET IT."
	inroom shute respond "What? And miss the excitement???"
;	roomis small respond "There isn't enough room to sleep in here."
	do .sleep
	nearto cosy print "You fall into a deep and relaxing sleep..."
	else print "You are now asleep."

; - - - - -

verb=.wake dream
syntax=any
	!infl me sleep end
	print "Your mind is suddenly filled with chaotic visions and manic chantings. A lemmon steps forward and bellows 'AWAKE!'."
	cure me sleep,action cansee "With a start, @me has woken up."

verb=wake dream
syntax=any,do !pre-magic
syntax=player
	action cansee MakeMag,do !rank5,print success
	infl player sleep senddaemon player .wake 0
syntax=player verb=up,syntax player none,syntax=verb verb=up,syntax none none
syntax=none
	!infl me sleep respond "You aren't asleep anyway!"
	active .deepsleep respond "You can't wake yourself up yet..."
	cure me sleep,print "You are now awake.",action cansee $50

; - - - - -

verb=.glow
syntax=any
	infl me glow respond "A sudden tingling feeling runs through your body..."
	print "You have magically started glowing."
	inflict me glow

verb=glow
syntax=noun,burns noun treatas light,error PeepsOnly
syntax=noun noun,burns noun treatas light,error PeepsOnly
syntax=any,do !pre-magic
syntax=player
	action cansee MakeMag,do !rank5
	print success,-> senddaemon player .glow 0
syntax=none
	infl me glow respond "You are already glowing!"
	action cansee MakeMag,do !rank4,print success,-> treatas .glow

; - - - - -

verb=.unglow
syntax=any
	!infl me glow respond "A sudden tingling feeling runs through your body..."
	print "You have suddenly stopped glowing."
	cure me glow

verb=unglow,syntax=any,do !pre-magic
syntax=player
	action cansee MakeMag
	!carrying cloak do !rank6,else print CloakShim
	print success,-> senddaemon player .unglow 0
syntax=none
	!infl me glow respond "You aren't glowing anyway!"
	action cansee MakeMag,do !rank4,print success,-> treatas .unglow

; - - - - -

verb=force
syntax=text player,syntax player text
syntax=player=me text,respond "No will power, eh?"
syntax=any,do !pre-magic
syntax=player text,action cansee MakeMag,do !rank7,print success,force player text
syntax=player verb,respond 'Try using force <player> "<text>".'

; - - - - -

verb=where
syntax=noun,action cansee MakeMag,do !rank5,print success,where noun1
syntax=verb,print "Try using WHERE <object>."

; - - - - -

verb=.sinvis
syntax=any
	infl me sinvis respond MagTingle,print "You have magically become super-invisible!"
	action global $23
	inflict me sinvis

verb=sinvis
syntax=any,do !pre-magic
syntax=player
	action cansee MakeMag,do !rank10,print success,-> senddaemon player .sinvis 0
syntax=none
	infl me sinvis respond "You are already super-invisible!"
	action cansee MakeMag,do !rank9,print success,-> treatas .sinvis

; - - - - -

verb=.unsinvis
syntax=any
	!infl me sinvis respond MagTingle,print "You are no-longer super-invisible."
	cure me sinvis
	action outside $20,action cansee "Out of nowhere, @me suddenly appears."
	action notsee "Inexplicably, you suddenly feel an awesome presence nearby..."

verb=unsinvis
syntax=any,do !pre-magic
syntax=player
	action cansee MakeMag,do !rank10,print success,-> senddaemon player .unsinvis 0
syntax=none
	!infl me sinvis respond "You aren't super-invisible anyway!"
	action cansee MakeMag,do !rank7,print success,-> treatas .unsinvis

; - - - - -

verb=.invis
syntax=any
	infl me invis respond MagTingle,print "You have magically become invisible!"
	action cansee "Before your very eyes, @me fades away into nothing!"
	inflict me invis

verb=invis
syntax=any,do !pre-magic
syntax=player,action cansee MakeMag,do !rank6,print success,-> senddaemon player .invis 0
syntax=none
	infl me invis respond "You are already invisible!"
	action cansee MakeMag,do !rank5,print success,-> treatas .invis

; - - - - -

verb=.uninvis
syntax=any
	!infl me invis respond MagTingle
	print "You have suddenly become visible again."
	cure me invis
	action cansee "Suddenly, out of nowhere, @me materialises out of thin air!"

verb=uninvis
syntax=any,do !pre-magic
syntax=player
	action cansee MakeMag
	!carrying cloak do !rank9,else print CloakShim
	print success,-> senddaemon player .uninvis 0
syntax=none
	!infl me invis respond "You aren't invisible anyway!"
	action cansee MakeMag,do !rank5,print success,-> treatas .uninvis

; - - - - -

verb=.cripple
syntax=any
	infl me cripple respond MagTingle
	print "You have suddenly become completely crippled!",inflict me cripple

verb=cripple
syntax=any,do !pre-magic
syntax=player,action cansee MakeMag,do !rank6,print success,senddaemon player .cripple 0
syntax=none
	infl me cripple respond "You are already crippled!"
	action cansee MakeMag,do !rank5,print success,treatas .cripple

; - - - - -

verb=.cure
syntax=any
	!infl me cripple respond MagTingle
	print "A sudden wave of power flows through you and you are no longer crippled!",cure me cripple

verb=cure
syntax=any
	do !pre-magic
syntax=player,action cansee MakeMag,do !rank4,print success,senddaemon player .cure 0
syntax=none
	action cansee MakeMag,!carrying cloak do !rank5,else print CloakShim
	print success,treatas .cure

; -------------------------- Combat Commands ------------------------------ ;

verb=swat	0HCAE
syntax=noun,checknear noun
syntax=noun=fly noun=swatter
	checknear noun1,!carrying noun2 respond NotGotTool
	print "With a single, deft sweep, you splatter the fly."
	destroy noun1,add 16 points me,action others "@xx has swatted the fly."
	-> setpre me "fly swatting"
syntax=noun=fly
	carrying swatter syntax noun swatter
	respond "The fly is too quick for you and you miss... If only you had a fly swatter..."
syntax=noun=swatter,checknear noun,respond "What would a fly-swatting swatter swat if he couldn't swat what a swatter swats with a fly-swatting swatter's swatter? Yes, you swat with a fly swatter, but you're supposed to tell me WHAT to swat."
syntax=noun,respond "Silly, only flies can be swatted, anything else you KILL."
syntax=none,!carrying swatter skip 1,nearto fly syntax fly swatter
	respond "You swat about like a moron, but it does you no good."
syntax=player,respond "That's somewhat pathetic."
syntax=player noun,syntax noun1 none

verb=wield
syntax=noun
	checknear noun
	!carrying noun error "You'll have to pick it up first!"
	!noun1 cutlass skip 1,myrank <2 respond WieldExp
	!noun1 axe skip 1,myrank <3 respond WieldExp
	print "You are now wielding the @n1."
	fighting me tell enemy "@me is now using @go @n1 to fight."
	wield noun

verb=fight
syntax=none,error "Calm down and try to be a little more concise, and maybe tell me WHAT to @vb..."
syntax=noun=fly,treatas swat
syntax=noun=fly noun=swatter,treatas swat
syntax=player=me,error "Keep your depressive tendencies to yourself, thankyou very much..."
syntax=player
	!cansee player error "You aren't within fighting distance!"
	fighting me error "You are already fighting @pl!"
	infl me cripple error "You can't attack anyone, you're crippled!"
	fight player,interact player,tell player "@xx is attacking you; you prepare to defend yourself..."
	print "You begin to attack @n1...",start .combat `7
syntax=player noun
	!carrying noun respond NotGotTool
	!noun2 cutlass skip 1,myrank <2 respond WieldExp
	!noun2 axe skip 1,myrank <3 respond WieldExp
	wield noun2,syntax player none
syntax=noun player,syntax player noun
syntax=noun noun,syntax noun1 none
syntax=noun
	checknear noun
	noun1 cat respond NoMobile,noun1 dog respond NoMobile
	noun1 cow respond NoMobile,noun1 fly respond NoMobile
	noun1 toad respond NoMobile,noun1 alien respond NoMobile
	noun1 badger respond NoMobile
	noun1 bear do !teddybear,& end
	respond "The @n1 seems utterly disintrested in your agressive advances."

verb=club
syntax=player noun=club,syntax player none
syntax=player
	!carrying club error "You can't CLUB someone without a CLUB."
	wield club,treatas fight

verb=stab
syntax=player noun=dagger,syntax player none
syntax=player
	carrying dagger wield dagger,& treatas fight
	carrying cutlass wield cutlass,& treatas fight
	error "You need a blade to stab someone..."

; Coming up next version: combat FIGHT | ONCE ... Once will allow you to do
; single turn combat sequences.

verb=kick
syntax=player=me,respond "I just wish I could!"
syntax=player
	!sameroom player error PlyrNotHere,!fighting me treatas fight
	respond "Be patient! You're already fighting, what more do you want!"
syntax=verb noun
	checknear noun,carrying noun respond "Put it down first."
	noun1 vase action others "@xx kicks the @n1 and smashes it.",&> respond "SMASH! It is no-more."
	noun1 cat Provoke Cat 0,& respond Dodges
	noun1 dog Provoke Dog 0,& respond Dodges
	noun1 cow respond "It looks at you SOOO pathetically that even a cruel, evil, miserable git like yourself can't quite bring themselves to kick it."
	noun1 fly respond "Practasing your Karate, eh?"
	noun1 toad Provoke Toad 0,& respond Dodges
	noun1 frog Provoke Frog 0,& respond Dodges
	noun1 badger Provoke Badger 5,& respond Dodges
	noun1 alien respond "Using some mystic power, the alien manages to prevent you from hitting it."
	noun1 bear respond "You kick it about a bit, but it refuses to withdraw from it's pacifistic stance. Aren't you just SOOO big, eh?"
	noun1 door print KickDoor
		&  action cansee "A rather large, green fairy appears and kicks @me into kingdom come. For your information, @ge tried to kick the door in."
		&> killme
	!noun1 wall respond "You'd probably damage it, and I'm afraid I just can't allow that."
	inroom tavclos respond "Try something a little more, eh, subtle... Like knocking, or tapping; bloody idiot."
	print "Ouch!",sub 2 stam me,action cansee '@me "accidentally" banged into the wall.'

verb=punch,syntax=any,treatas kick

verb=hit,syntax=any,treatas kick

verb=thump,syntax=any,treatas kick

verb=flee
syntax=any
	!fighting me respond "You aren't fighting anyone anyway!"
	infl me cripple respond "You aren't going ANYWHERE - you're crippled, dim wit!"
syntax=verb verb
	cancel .combat
	visibleto enemy tell enemy "@me has fled @n1.",else tell enemy "Your oponent has fled!"
	add myrank points enemy
	flee,print "You have fled, coward...",setpre me "cowardly"
	sub myrank points me,treatas noun1
syntax=any,respond "You have to tell me which way to flee."

; ----------------------- Communication Commands -------------------------- ;

verb=tell,syntax=any,treatas !speech

verb=ask,syntax=any,treatas !speech

verb=say,syntax=any,treatas !speech

verb=shout
syntax=none,announce global UEmote,print 'OK, @me @vbs at the top of @go voice!'
syntax=text,announce global '@xy @vbs "@n1"',print 'OK, @me @vbs "@n1"'

verb=scream,syntax=any,treatas shout

verb=blow
syntax=noun,checknear noun
syntax=noun=horn
	state noun 3 announce global "A single, dull note drifts through the air.",& respond "It produces a low, dull note."
	state noun 2 announce global "A single note of a horn drifts unmelodiously through the air.",& respond "It produces a single, unmelodious note."
	inc noun announce global "You hear the crystal clear call of a hunting horn.",& print "It produces a crystal clear note, which any huntsman would be proud of.",&> add 25 points me
	respond "Not a sound."
syntax=noun
	! noun1 whistle respond "Blow all you like - nothing happens!"
	! carrying noun respond NotGotNoun
	announce others "!PEEEEEEEEEEEP! @xy whistles."
	announce outside "!PEEEEEEEEEEEP! You hear a whistle blown in the distance..."
	respond Whistle
syntax=verb,print "Awh, things must be hard..."

verb=wail,syntax=any,print IEmote,announce global UEmote

verb=moan,syntax=any,treatas wail

verb=cheer,syntax=any,treatas wail

!greet
syntax=player
	interact player		; So he doesn' get ACTION OTHERS stuff
	print "OK, you @vb @n1.",toprank add 16 points player,& end
	myrank >#player add myrank points player,& sub 2 points me

verb=welcome		; When you greet someone/thing to The Land...
syntax=none,respond "Ahh, nice of you to notice me."
syntax=noun,respond "The @n1 doesn't understand, but I'm sure it thanks you, anyway."
syntax=player=me,respond "I'm sure you would."
syntax=player,sameroom player tell player XYVbsYou,else tell player MEVbsYou,do !greet

verb=greet
syntax=player=me,respond "OK. Greetings, fool."
syntax=player,treatas welcome

verb=thank
syntax=none,respond "You're welcome. What did I do (don't answer that)."
syntax=player=me,respond "Thank YOU for nothing!"
syntax=player,tell player XYVbsYou,do !greet

verb=console
syntax=none,error "We don't talk to computers hereabouts, we type to them."
syntax=noun,!noun1 cow checknear noun,respond "Well, I hope it appreciates that."
syntax=player=me,start .cons 6,respond '"YOU ARE A COMPLETE AND UTTER SCHMUCK; I DETEST YOU". Make you feel any better?'
syntax=player,tell player MeVbsYou,do !greet

verb=encourage
syntax=noun,respond "Took me long enough to get the thing just to exist, and your measily effort makes it do little else."
syntax=player=me,respond "OK, you're not such a bad guy, infact I even reckon you'll get places. (Hopefully some time soon you'll learn more interesting commands, like North, South...)"
syntax=player,treatas console

verb=bless
syntax=none,respond "Gesundheit would have done."
syntax=player=me,respond "OK, gesundheit."
syntax=noun,treatas console
syntax=player,tell player "@xy @vbes you.",do !greet

verb=pardon
syntax=player=me,respond "Why, what have you done???"
syntax=none,respond "Did you say something?"
syntax=any,treatas console

verb=congratulate
syntax=player=me,respond "What do you want? Pat on the back or a kick in the teeth?"
syntax=noun,treatas console,syntax=player,treatas console

verb=kiss
syntax=none,error "No thanks."
syntax=player=me,error "Sorry, I only kiss OLD wart hogs."
syntax=player,tell player "@xx @vbes you.",do !greet,-> action cansee "@xx gives @n1 a terrific @vb."
syntax=noun
	checknear noun
	noun1 toad skip 1,!noun1 frog respond "No thanks."
	!state noun 0 respond "Yeuch, don't be disgusting - it's DEAD."
syntax=noun=toad
	setpost me "with a face full of warts"
	mysex male respond "Erm, you don't kiss TOADS you kiss FROGS, and the idea is that they turn into a PRINCE. Dear dear, SOME people! Besides, TOADS give you warts..."
	respond "Gotcha! It's FROGS not TOADS that you're supposed to kiss. You simply contract a few warts."
syntax=noun=frog
	send frog heaven,mysex male print MKissFrog,& add 30 points me
	else print FKissFrog,& add 60 points me,& setpre me "love striken"
	action others "@xx kisses the frog and there is a POOOF of purple smoke. When it finally clears, the frog has gone."

verb=snog,syntax=any,treatas kiss

verb=hug
syntax=player=me,respond "Gimme a break, gizzard face."
syntax=none,error "Whom or what do you want to @vb?"
syntax=player,tell player "@xx @ves you.",do !greet,-> action cansee "@xx gives @n1 a terrific @ve."
syntax=noun=bear
	!carrying bear print NotGotNoun
	setpre me "cuddly"
	respond "Awww, cute, but don't think I'm giving you any points for it. The stuffing seems a bit hard!"
syntax=any,treatas console

verb=cuddle,syntax=any,treatas hug

verb=pat
syntax=verb noun
	checknear noun
	noun1 dog respond PatDog,noun1 cow provoke cow 0,& respond PatCow
	noun1 cat provoke cat 0,& respond PatCat
	noun1 badger provoke badger 0,& respond Dodges
syntax=any,treatas hug

verb=stroke
syntax=verb noun
	checknear noun
	noun1 dog respond StrDog,noun1 cow respond StrCow
	!noun1 cat skip 2,lastverb pat respond StrPCat,respond StrCat
	noun1 badger provoke badger 0,& respond Dodges
	noun1 statue provoke cow 0
syntax=any,treatas hug

verb=introduce
syntax=player player=me,syntax player2 player1
syntax=player=me player,respond "Why not just WELCOME @n2?"
syntax=player player
	!cansee player1 error PlyrNotHere,!cansee player2 error "@n2 isn't here!"
	print "OK, you introduce @n1 to @n2."
	tell player1 "@me introduces you to @n2.",tell player2 "@me introduces you to @n1."
	myrank <#player1 end,myrank <#player2 end
	add 6 points player1,add 6 points player2,!toprank add 3 points me

verb=hi
syntax=none,respond "Awwh, you noticed me!"
syntax=noun=all,action global "@me bids everyone '@vb'!",respond YouBidAll
syntax=player=me,respond "Dimwit, you're supposed to greet OTHER people..."
syntax=player,tell player IBidYou,respond YouBid

verb=hello,syntax=any,treatas hi

verb=bye
syntax=none,print "And goodbye from me!",save,treatas quit
syntax=any,treatas hi,save,treatas quit

verb=byebye,syntax=any,treatas bye

verb=ciao,syntax=any,treatas bye

verb=farewell,syntax=any,treatas bye

verb=follow
syntax=noun,error "We don't do mobiles yet, so its inanimate, berk!"
syntax=player=me,respond "But I do! Just try and get rid of me!"
syntax=player
	!cansee player respond PlyrNotHere
	interact player,follow player,print "OK, you are now following @n1."
syntax=none,error $45

; ---------------------------- Glamour Verbs ------------------------------ ;

verb=leave
syntax=none
	print "Try using 'out' or moving in the direction in which you wish to leave."

verb=reset,syntax=none,respond "Next reset is due in @re."

verb=go
syntax=verb verb,treatas noun1
syntax=noun,checknear noun
syntax=noun=crack
	inroom slopetun3 treatas east,else treatas west
syntax=noun,noun1 doorway respond GoDoor,noun1 door respond GoDoor
syntax=none,error "No, YOU go!"

verb=crash
syntax=any,respond "PRECIOUS does that quite often enough on its own, thankyou very much..."

verb=q
syntax=none,respond "Too many [stupid] people type 'Q' by accident, so the abbreviation for QUIT is QQ."

verb=pi
syntax=none,respond "3.14159, or thereabouts."
syntax=noun,treatas get
syntax=noun verb=up,syntax noun1 none

verb=sit
syntax=noun=bed
	checknear noun,state noun 0 respond Uncomfy0
	state noun 0,respond Uncomfy1
syntax=noun
	checknear noun,error "Sorry, this stuff wasn't provided as a rest for your backside, pal!"
syntax=any
	sitting error "You already are!"
	lying error "You're already lying down!"
	sit,action cansee "@me has just sat down."
	print "Amazing, you're now sitting down."

verb=lie
syntax=noun,treatas sit
syntax=any
	lying error "You're already horizontal!"
	sitting error "What for? You're obviously quite happy sitting down."
	lie,action cansee "@me has just laid down."
	print "You're now laid down."

verb=stand
syntax=noun
	checknear noun
	noun1 vase destroy vase,& respond "The delicate little vase shatters into a thousand pieces when you put your hefty weight onto it."
	error "Look here, pal, find yourself a soap box in someone elses world!"
syntax=any
	sitting skip 2,lying skip 1,error "You're already stood up!"
	infl me cripple respond "You can't move an inch - you're crippled!"
	asleep respond "You'll have to wake up first!"
	stand,action cansee "@me is now stood up."
	print "Righto! You're now stood up!"

verb=answer
syntax=noun,checknear noun
syntax=noun=phone
	state noun 0 respond "It's not ringing ... Infact it's not even connected!"
	state noun 1 action others "@xx grabs the phone, then slams the receiver down angrily."
	& state noun 0,& add 5 points me
	& respond "<Click>. The caller hangs up JUST as you made it. Angrily, you slam the receiver back down."
	state noun 0,action others "@xy has answered the phone."
	print AnswerPhone,add 50 points me
syntax=verb text player,syntax noun2 noun1
syntax=verb player text,treatas !speech
syntax=none,error $45

verb=use
syntax=noun,checknear noun
syntax=noun=phone
	state noun 2 respond "You'd better answer it first!"
	respond "It's useless... There's no dialing tone or anything... Perhaps the cave-men weren't in when BT called?"
syntax=noun,respond "You have to figure out how to."
syntax=none,error $45

verb=listen
syntax=any
	infl me deaf error "You can't hear a THING! You're deaf, remember?"
syntax=noun=phone
	state phone 2 skip 2
	 nearto phone respond "It makes not the slightest noise."
	 respond "You can't hear one!"
	nearto phone respond "It's ringing - maybe you ought to answer it?"
	respond "Somewhere in the distance you hear one ringing..."
syntax=any
	state phone 0 respond "All is still..."
	nearto phone respond "The phone is ringing - ANSWER IT!"
	nearto phonering respond "Somewhere nearby there's a phone ringing..."
	nearto phonedist respond "You can hear a phone ringing quite clearly somewhere in the distance..."
	respond "There is a wonderful quietness spoiled only by the distant ringing of a phone!"

verb=.bwind
syntax=any
	announce others "@xx just broke wind.",respond "You just broke wind!"

!checkpee
syntax=any
	active .cantpee error "Try as you might, your bladder is completely empty!"

verb=urinate
syntax=noun=loo
	!nearto loo error "You'll have to find one, dimwit.",syntax none none
syntax=noun
	checknear noun
	noun1 basin error "That's disgusting, I won't let you do it!"
	!lit noun respond DontBeVulg
syntax=noun=stove
	do !checkpee
	action cansee "BLECH! @me has peed in stove!",start .bwind `20,setpre me vulgar
	respond "The flames are magical, so nothing special happens, it just generates steam."
syntax=noun=fire
	do !checkpee
	action cansee "@me has peed on the fire.",start .bwind `20,setpre me vulgar
	respond "It's a magical fire, so nothing happens. It does warm your genitals a little, though."
syntax=noun
	noun1 dish respond "It's supposed to contain COWS milk, not YOUR milk."
	respond DontBeVulg
syntax=player=me
	setpre me "disgusting, filthy, sick", respond "Some people have some STRANGE fetishes. No thanks."
syntax=player
	!sameroom player error "I WON'T ask how you think that would be possible..."
	do !checkpee,interact player
	tell player "@xy just peed on you.",action others "@xy just peed on @n1."
	setpre me vulgar,respond "I don't think @n1 liked that..."
syntax=none
	do !checkpee
	start .cantpee 90
	!nearto loo start .bwind `20,& respond "You could atleast have done it in the toilet."
	print "It's nice to find someone with some manners... Aaaahh! What a relief."
	announceto guestw "From nearby you hear a long, loud sigh of relief."
	add 4 points me, state loo 1, setpre me "hygienic",start .bwind `50

verb=flush
syntax=noun noun
	! noun2 loo error "You can't flush things down the @n2!"
	! carrying noun1 error NotGotNoun
	send noun1 store,objannounce hotelrms LooFlush,respond "Whooosh! It's gone!"
syntax=noun
	noun1 loo syntax none none,checknear noun,respond cantvbthat
syntax=none
	!nearto loo respond "I think you mean BLUSH."
	objannounce hotelrms LooFlush
	!dec loo respond "Whoosh... Great fun, what now?"
	print "WHOOSHHH... Someone had to do it!", add 4 points me

verb=drink
syntax=any
	inroom underlake cancel .drown, & print "Silly billy, your body confuses drinking for breathing and, well, being under water...", & killme
syntax=none,nearto water syntax water none,else error "There's nothing to drink here."
syntax=noun=dish noun=dish,syntax noun none
syntax=noun
	checknear noun
	!noun1 dish skip 3	; Includes 'drink milk'
	 !dec noun respond "The dish is empty!"
	 nearto cat respond "The cat watches pitifully as you drink it's particular favourite tipple."
	 respond "Ahhh! There's nothing quite like fresh milk."
	!noun1 water error CantVbThat
	inroom wloos skip 2,cancel .cantpee,respond "Very refreshing."
	state loo 0 setpost me "and sicko"
	& sub 2 str me,& respond "Yeuch! You don't know whats been in there!"
	setpost me "and pervert"
	sub 8 str me,respond "BLECH! You KNOW what WAS in there, don't you!"

verb=swim
syntax=any
	inroom underlake print "You swim to the surface...",& treatas up
syntax=noun
	checknear noun
	noun1 loo respond "No, no. That's NOT what you do in a latrine."
	; If they swim in the spring, clear their setpre and setpost's.
	noun1 spring setpre me "", setpost me "",respond "The water is lovely, and you feel cleansed."
	ishere river respond "The river is flowing too fast for you to swim in it safely."
	noun1 water syntax none none
	respond "You can't swim in that."
syntax=none
	!ishere water error "You need water to swim."
	inroom gardwell respond "The well is dry, dimwit. Try climbing down it."
	inroom bathroom respond "<Cough> Bath-ROOM not a swimming bath."
	inroom eloos respond "Erm, that's a basin, not a pool."
	inroom wloos respond "You can't quite fit into the toilet basin, so you give up."
	inroom wlake setpre me "wet",respond "You splash about a bit and get in that horrible state they call 'wet'."

verb=.buttn1
syntax=any
	print "CLICK! Nothing seems to happen... But then suddenly the floor dissapears from beneath your feet..."
	announce cansee "With a slight WHOOSH, @me suddenly dissapears into the ground!"
	move shute
	announce others "You hear several bumps and muffled shouts coming from further up the shute..."
	start .bumpshute 7

verb=.raisepc
syntax=any
	dec portcullis objaction portcullis "The portcullis has been raised."

verb=.lowerpc
syntax=any
	inc portcullis objaction portcullis "The portcullis has been lowered."

verb=.buttn2
syntax=any
	print Nothing,state portcullis 0 gstart .lowerpc `8,else gstart .raisepc `8

verb=press	?HCAE
syntax=none,error $45
syntax=noun,checknear noun
	!noun1 door skip 2
		state door 0 respond "Try CLOSEing it."
		else respond "Try OPENing it."
	!noun1 button error Nothing
	inroom bedrm2 do .buttn1,& end
	inroom councch do .buttn2,& end
	respond NotThatKind

verb=push
syntax=none,error $45
syntax=noun noun=cliff
	!nearto cliff respond "Nutter, there's no cliff here!",syntax noun none
syntax=noun
	checknear noun,noun1 button treatas press
	noun1 door respond "Don't be chicken, just go-on and open it, or close it if you like!"
	noun1 cow provoke cow 0,& respond "It doesn't like being pushed, but it gets the idea..."
syntax=noun=seesaw
	inc noun gstart .raisepc `20,& action others "@xx tips the seesaw to the right.",& respond "Squeakily, the seesaw tips to the right."
	state noun 0,gstart .lowerpc `20,action others "@xx tips the seesaw to the left.",& respond "With a fair deal of squeaking, it tips to the left."
syntax=noun
	!inroom gcliff respond "It doesn't do much at all."
	carrying noun respond "Not a good idea to fidget so close to the cliff's edge."
	send noun store,action others "@xx has pushed the @n1 over the cliff edge."
	print "Wheeee! That wasn't fun at all.",
syntax=player=me noun=cliff,!nearto cliff respond "Lemming."
syntax=player=me,respond "Some people are ridiculous."
syntax=player noun=cliff
	!nearto noun respond "Dimwit, there's no cliff round here."
	syntax player none
syntax=player
	!cansee player respond PlyrNotHere
	interact player, print "OK. You just pushed @n1."
	!inroom gcliff tell player "@xx just pushed you.",& end
	tell player "@xx just pushed you, nearly making you lose your ballance."
	senddaemon player .gotpushed 2

verb=play
syntax=noun
	checknear noun,do !burnme
	noun1 horn error "You don't PLAY a horn, you just BLOW it."
	!noun1 violin error CantVbThat
	!state noun 0 respond "It's damaged beyond playing."
	!carrying bow state noun 1
	& respond "Its a delicate instrument, and such rough handling damages it beyond repair. You should have used a bow."
	state noun 2 respond "You can't, its SOLID gold, strings and all!"
	!inroom bandstand respond "It produces a lovely melody, but somehow it has now depth or feeling to it."
	print PlayVio1,announce global PlayVio2
	state noun 2,settask 3,showtasks,-> add 35 points me
syntax=player,respond "Really, keep your perverse ideas to yourself!"
syntax=none,treatas nod

verb=milk
syntax=noun
	checknear noun,do !burnme,noun1 cat respond "That's probably want the cat wants ... TO DRINK."
	noun1 dog provoke dog 0,&> respond "It's a male dog, I DOUBT you'd want to drink the milk it produces."
	noun1 badger provoke badger 0,& respond "Badgers are well known for their ferocity, but fortunately this one just gets spooked by your strange intentions."
	!noun1 cow respond "Fetid moron, the @n1 isn't a milk producer."
	!carrying dish respond "Milking a cow is fine, but unless you milk it into something, the milk simply goes all over the floor, as you just found out."
	state dish 2 print "You milk the cow into the golden dish, only to find that it leaks and the milk is all lost. Obviously the gold isn't as solid as you thought."
	state dish 1 print "After a moment or two you get the milk flowing, but you have nowhere to catch the milk, and it's all wasted, and within a moment it has all dissapeared."
	state dish 0 print "After a moment or two you get the milk flowing, and using the dish to catch it, you soon have a dish full of milk.",& state dish 1
	announce here "The cow lets out a MOO of relief."
syntax=noun noun
	!noun1 cow syntax noun1 none,noun2 dish syntax noun1 none
	respond "You want something better designed for holding milk, like a bucket, or a bowl or something."
syntax=none,error $45

verb=!munch
syntax=noun
	destroy noun
	noun1 kipper respond "Whoever cooked this kipper did it some time ago. Fortunately you manage to keep it down."
	full stam me error "Not bad, but you weren't hungry."
	fix stam me
	noun1 potatoe respond "Hmm... Raw potatoe! It'll do, I suppose."
	noun1 apple respond "MMMMmmm! Golden delicious, just right!"
	noun1 wafer respond "Bland, but nourishing."
	respond "Hmm... Just the trick, if not the flavour."

verb=eat
syntax=noun
	checknear noun,!carrying noun error NotGotNoun
	noun1 potatoe	treatas !munch,	noun1 carrot	treatas !munch
	noun1 wafer	treatas !munch,	noun1 apple	treatas !munch
	noun1 apricot	treatas !munch,	noun1 biscuit	treatas !munch
	noun1 kipper	treatas !munch,	noun1 cake	treatas	!munch
	noun1 herring	respond RedHerr
syntax=noun=brand
	state noun 0 print "We're not a fire-eater, I'm afraid, and you burn your mouth!"
	&> sub 1 str me
	respond "With ideas like that, I think I MAY have discovered artificial intelligence."
syntax=noun
	respond CantVbThat
syntax=player,respond "Now that's just a LITTLE stupid, my friend."
syntax=none,error $45

; This could POSSIBLY call send the !eat verb to 'other' players!?!
verb=feed
syntax=noun
	checknear noun,noun1 dog respond "He seems totally uninterested."
	noun1 cow respond "In a cow like way, she lets you know that her cud will do just fine."
	noun1 fly respond "We ALL know what flies eat, so let's have none of that, eh?"
	noun1 rat respond CantGetClose
	noun1 badger respond CantGetClose
	!noun1 cat respond CantVbThat
	!carrying dish respond "The cat sniffs at it and mews, unhappily, but doesn't touch it."
	else> treatas !feedcat
syntax=noun noun
	checknear noun2,checknear noun1
syntax=noun=cat noun=dog
	provoke cat 0,respond "The cat is already nervous because of the dog, and it isn't too comfortable about you."
syntax=noun=kipper noun
	!noun1 cat respond "It isn't interested in being fed, thanks."
	respond "It sniffs it, eyes you for a moment, and the decides there must be a catch."
syntax=noun noun
	noun1 dish syntax noun2 none,else syntax noun1 none
syntax=noun player
	!cansee player respond PlyrNotHere,respond CanFeedSelf
syntax=player
	!cansee player respond PlyrNotHere,respond CanFeedSelf
syntax=none,error $45

verb=read
syntax=noun
	checknear noun,! light respond "It's too dark to read anything here."
	noun1 wallpaper respond ExamWPaper,noun1 paper treatas examine
	noun1 sign respond ExamSign
	noun1 graffiti respond ExamGraf
	noun1 notices respond notices
syntax=noun=ghost noun=book,syntax noun2 noun1
syntax=noun=book noun=ghost
	checknear noun2,checknear noun1,syntax noun1 none
syntax=noun=book
	!ishere ghost respond "It contains several, beautiful hymns. One that especially catches your attention is called 'My Shepherd' which is a delightful interpretation of Psalm 23 by a guy called Michael Card."
	print ReadToGhost,send ghost heaven,send book store
	announce others "@xx reads several verses hymns to the ghost. Suddenly the ghost gives a shout of joy, and vanishes for ever!"
	action outside "In the distance you hear a shout of joy!"
	taskdone 2 skip 2,!toprank settask 2,& showtasks,add 80 points me
syntax=none,error $45

verb=ring
syntax=player verb=up,syntax player none
syntax=player
	!nearto phone respond '"And Noddy says to Big Ears..."^MOh sorry, were you speaking to me?'
	respond "Ahem, this phone isn't connected. Perhaps I didn't point it out, or perhaps you just didn't read the description..."
syntax=none,treatas laugh

verb=wait dream
syntax=noun=phone verb=ring,syntax phone none
syntax=noun=bus
	!lastverb wait syntax none none,respond "I was only joking, nerd."
syntax=noun=phone
	!ishere phone respond WaitPhLong,respond WaitPhone
syntax=noun
	noun1 cow syntax cat none,noun1 dog syntax cat none
	noun1 fly syntax cat none,noun1 rat syntax cat none
	noun1 frog syntax cat none,noun1 toad syntax cat none
	noun1 alien syntax cat none,noun1 ghost syntax cat none
	noun1 badger syntax cat none
	!noun1 cat respond WaitBad
	!ishere noun respond "I see, you're going to jump it. Righto. Meanwhile, the world carries on!"
	respond WaitLeave
syntax=noun verb=leave,checknear noun,respond WaitLeave
syntax=player verb=leave
	!cansee player error PlyrNotHere
	respond "[Whispers] Why? Don't tell me, the world is going to end when @n1 leaves this place? Course, maybe you want to 'Follow @n1'?"
syntax=number,respond WaitBad
syntax=none,respond "The bus isn't due for atleast another half an hour!"

verb=no
syntax=none
	lastverb sleep respond GoodThen
	lastverb force respond "Poor thing. I'm still not going to force you to do anything."
	lastverb console respond "Awwwh, bless your little cotton socks."
	lastverb wait respond "Well, don't asking me to go waiting till they leave then."
	lastverb pardon respond "Must be hearing things!"
	respond "Don't be negative, it suits you."
syntax=player,tell player FirmTell,print "OK, {",print FirmTell

verb=yes
syntax=none
	lastverb sleep respond TuffLuck
	lastverb force respond "Well, you don't need me to force you about then, eh?"
	lastverb console respond "Good, I like MORONS."
	lastverb wait respond "Well, type it in the, dimwit."
	lastverb pardon respond "Oh, you'll have to say it again then."
	respond "Nice to see you being positive."
syntax=player,tell player FirmTell,print "OK, {",print FirmTell

verb=worship
syntax=noun,respond "Is the @n1 REALLY worthy of your praise??? Nope, I don't think so..."
syntax=none,inroom confessnl skip 1,!inroom prayroom respond "Try as you might, you can't get into a proper attitude of worship here."
	respond "You worship God Almighty from the very bottom of your heart, and the experience leaves you feeling clean and peaceful."
syntax=player=me,respond "Huh! FAT CHANCE! My hero is the Motorola 68060 CPU!"
syntax=verb player,respond "Ok, so you like @n1! Why not just tell them?"

verb=fly,syntax=none,respond "You flap your wings frantically..."

verb=knock
syntax=noun=wall
	inroom tavclos announce others "@xx @ves on the wall.",& do !powsleep
		& randomgo any,& start .glow 45,&> action others SleepArr
syntax=noun,checknear noun,do !burnme
syntax=noun=door
	print "@ve, @ve, @ve..."
	interact me		; Stop ME getting the ObjAction message!
	objannounce noun "@xx @ves."
	!nearto doorport end,else do !powsleep
	inroom cpass4e1 move sqroom,else move cpass4e1
	-> action cansee SleepArr
syntax=noun
	noun1 wall respond "It seems pretty solid."
	print "@ve ... @ve ... YAWN."

verb=dig
syntax=verb noun noun=spade
	checknear noun2,!carrying noun2 error "You'll have to pick the @n2 up first!"
	syntax noun1 none
syntax=noun=spade,checknear noun,!carrying noun error NotGotNoun,syntax none none
syntax=noun,checknear noun
syntax=verb noun=ring
	inroom ugfring syntax none none
syntax=verb noun,error CantVbThat
syntax=verb none
	!inroom ugfring error "You can't dig here."
	!carrying spade error "You aren't carrying anything to dig with."
	!dec !fring error "There's no where left to dig here now."
	in store tiara print DigRing1a,& action others DigRing1b,& send tiara here
	else print DigRing2a,& action others DigRing2b
	add 30 points me

verb=wash
syntax=verb noun noun
	checknear noun2,inroom spring1 syntax noun none
	noun2 water syntax none none
	noun2 loo error "Ahem; people with brains don't wash things in toilets."
	error CantVbThat
syntax=verb noun
	checknear noun
	noun1 dog provoke dog 0,& respond "That's an incredibly good idea, but the dog catches on and dodges you."
	!ishere water error "You need some water to clean it!"
	noun1 paper destroy noun,& respond "It does the paper no-good at all, it simply disintegrates immediately."
	noun1 spade respond "It was already rusty, it doesn't need your help."
	noun1 violin respond "In your world maybe you do clumsily wash rare and delicate artifacts, but not in mine."
	noun1 portrait respond "That's incredibly stupid! As if you wash a fine painting like that! I can see you're not a coniseur..."
	noun1 trophy respond "I think this is leading to a bout of ego. Well, it's not YOUR trophy."
syntax=verb noun=bear
	state noun 0 respond "And get it all wet? It's a stuffed toy! It'd catch pneumonia!"
	respond "Don't bother, you'd only add to it's miseries."
syntax=verb noun=brand
	state noun 0 print "Very clever; it goes out, ofcourse."
	state noun 1 print "Not a good idea; you'll never get that to light now!"
	-> state noun 2
syntax=verb noun,!inroom spring1 error WaterDirty
syntax=verb noun=brooch
	!dec noun error Washed
	add 5 points me,respond "It comes up sparklingly clean!"
syntax=verb noun=wand
	!dec noun print DipWand1,& sub 5 stam me,& setpre me "incredibly stupid wand washing",&> destroy noun
	add 10 points me,respond DipWand2
syntax=verb noun=pan
	!dec noun respond "It's still sparkling clean, what did you expect? Gold?"
	print "The spring water really must be magic to get that stuff off the pan, but it manages it! You have done a service to mankind getting that thing clean!"
	&> add 8 points me
syntax=verb noun=horn
	state noun 0 error "Only a fool would put a beautiful horn like that into that mucky water!"
	state noun 1 error "Behave! It's solid gold, you couldn't get it cleaner if you spent the rest of your life cleaning it!"
	state noun 2 error "You wash, and rub, and clean, and polish, but it doesn't come up any cleaner... Perhaps it needs something, well, more..."
	dec noun print "Much better, but it's still quite dull; it's obviously suffered a great deal of neglect.",&> add 5 points me
syntax=verb noun
	noun1 kipper respond "Hmmm. Perhaps you wanted it unsmoked? Well, it didn't work."
	respond "Nice try, but it doesn't do it any obvious good."
syntax=verb player=me,respond "Kinky. Do it yourself."
syntax=verb player,respond "Leave their personal hygiene up to them."

verb=!mobzap
syntax=noun noun=wand,syntax noun1 none
syntax=noun
	action others SeeZapMob,print IZapMob
	add ?noun1 points me		; Get the mobiles 'worth'
	send noun heaven,destroy noun	; Send it to heaven and destroy it.

verb=zap
syntax=none,respond "Zowee!"
syntax=any,!carrying wand error NeedWand
syntax=verb player=me,respond "Depressing. It (you) really is."
syntax=verb noun noun,noun2 wand syntax noun1 none,else error "You zap things with a wand, not the @n1."
syntax=verb player noun
	!noun2 wand error NeedWand
	checknear noun2,!carrying noun2 respond NotGotTool,syntax player none
syntax=verb player
	! cansee player error "@n1 isn't within the wand's range..."
	! state wand 0 error "Your hand tingles a little when you try, but nothing else happens."
	interact player
	! visibleto player tell player "You are hit by large, blue bolt of electricity out of nowhere."
	& print "A large, blue bolt of electricity shoots out of the wand and hits @n1 unawares."
	else tell player "@me zaps you with a large, blue bolt of electricity from @go wand."
	& print "@n1 is unable to dodge the large, blue bolt of electricity which shoots out of the wand."
	add #player points me
	myrank <4 sub 8 stam player,& end
	myrank <8 sub 12 stam player,else sub 16 stam player
syntax=verb noun
	checknear noun
	noun1 cat do !mobzap,& end,	noun1 dog do !mobzap,& end
	noun1 rat do !mobzap,& end,	noun1 toad do !mobzap,& end
	noun1 frog do !mobzap,& end,	noun1 fly do !mobzap,& end
	noun1 alien do !mobzap,& end,	noun1 cow do !mobzap,& end
	noun1 badger do !mobzap,& end
	noun1 ghost respond ZapGhost
	!burns noun respond "There is a magical sparkle at the tip of the wand, but nothing else happens."
	lit noun respond "Nothing at all happens."
	treatas light		; Light the object.

; -------------------------- Emotion Commands ----------------------------- ;

verb=rofl		; roll on the floor with laughter
syntax=none
	print "OK, @me the @mr rolls on the floor with laughter.",action cansee Rofl

verb=act		; The almighty command!
syntax=text,action cansee "@xy @n1",respond "OK, @me the @mr @n1"
syntax=any,respond 'No, no, no, not like that... Try something like: @vb "has realised how to use act." - other people see the same as you without the "OK".'

; The two basic emotion commands are 'nod' which performs an action and
; 'laugh' which makes a noise.. There IS a difference!

verb=beep
syntax=verb,print "OK, ^gB^gE^gE^gP^g!^g",announce others "@xx beeps ^gB^gE^gE^gP^g!^g"

verb=!wavex
syntax=verb noun
	print "The @n1 seems to collect all nearby static electricty, which shoots down your arm. Automatically, you drop it, but too late..."
	drop noun,do !powsleep
syntax=verb noun=baton
	nearto bow move welchall,&> action cansee sleeparr
	!exists bow move welchall,else move *bow
syntax=verb noun=bow
	nearto baton move welchall,&> action cansee sleeparr
	exists baton move *baton,else move welchall
syntax=verb noun
	action cansee sleeparr

verb=wave
syntax=player=me,respond "Done. Did you enjoy that?"
syntax=player
	!sameroom player respond "Somehow I doubt that @n1 appreciated that."
	!visibleto player respond "@n1 can't see you, dumbo!"
	!cansee player respond PlyrNotHere
	interact player,tell player "@xx @vbs to you.",action cansee UEmote,respond "OK, you @vb to @n1."
syntax=noun
	!carrying noun respond NotGotNoun,action cansee "@me the @mr @vbs the @n1."
syntax=noun=wand
	state noun 0 respond "It generates a definitely magical hum as it slices through the air; but nothing else happens."
	respond "It glows a little, but it looks like it's had it."
syntax=noun
	noun1 bow do !waveX,& end,	noun1 baton do !waveX,& end
	!noun1 brand skip 2
		state noun 0 print "Unfortunately all that waving it about extinguishes the @n1."
		-> state noun 1
	respond "OK, @me the @mr @vbs the @n1.",
syntax=any,treatas nod

verb=tap
syntax=verb player
	!sameroom player error "I can't see @n1."
	tell player "@xx taps you.",respond "You tap @n1."
syntax=verb noun,treatas knock
syntax=any,treatas laugh

verb=grin,syntax=any,treatas nod

verb=stagger,syntax=any,treatas nod

verb=blink,syntax=any,treatas nod

verb=frown,syntax=any,treatas nod

verb=smile,syntax=any,treatas nod

verb=wince,syntax=any,treatas nod

verb=wink,syntax=any,treatas nod

verb=swoon,syntax=any,treatas nod

verb=wobble,syntax=any,treatas nod

verb=grimace,syntax=any,treatas nod

verb=hop,syntax=any,treatas nod

verb=duck,syntax=any,treatas nod

verb=shudder,syntax=any,treatas nod

verb=cower,syntax=any,treatas nod

verb=yawn,syntax=any,treatas nod

verb=quiver,syntax=any,treatas nod

verb=smirk,syntax=any,treatas nod

verb=blushe,syntax=any,treatas nod	; so we get 'blushes' not 'blushs'

verb=itche,syntax=any,treatas nod

verb=protest,syntax=any,treatas nod

verb=confirm,syntax=any,treatas nod

verb=beg,syntax=any,treatas nod

verb=apologise,syntax=any,treatas nod

verb=smell
syntax=noun
	carrying noun skip 1,!ishere noun respond "I can't see the @n1, let alone smell it!"
	do !burnme
	noun1 breeze respond "Sniff sniff ... Just smells musty!"
	noun1 kipper respond "Smells, how can I put it, fishy?"
	noun1 herring respond "C'mon, it's a red herring. It smells fishy, of course!"
	noun1 pan print "Eeek! The smell is almost, no, is overwhelming! {"
		& do !zonksleep
		& action cansee "@me sniffs @go pan, keels over and dematerialises inront of your eyes!"
		& randomgo any
		&> action cansee SleepArr
syntax=noun=loo,state noun 0 respond "It reeks of fresh urine! UGH!",respond "It doesn't smell clean, but it doesn't smell foul!"
syntax=noun=brand
	state noun 0 print Sniffst1,&> killme,else respond SniffST2
syntax=noun=cat,state noun 0 respond CantGetClose,respond SniffDC1
syntax=noun=dog,state noun 0 respond CantGetClose,respond SniffDC1
syntax=noun,respond "It doesn't smell anything special."
syntax=any,treatas nod

verb=skip,syntax=any,treatas nod

verb=dance
syntax=player=me,respond "The music strikes up, and we do a superb, slow waltz together... IN YOUR DREAMS!"
syntax=player
	!cansee player tell player "You feel a peculiar urge to dance with @me."
	&		error "How sad, @n1 isn't around to dance WITH you."
;	!fighting me skip 2
;		noun1 enemy stopfight
;		noun1 enemy error "Stupid, your are in mortal conflict with @n1, this is no time for soft-shoe."
	fighting me error "Pull yourself together! This is no time for dancing! You must have taken quite a serious blow to the head."
	interact player
	tell player "@xx takes you by the hand and leads you in a merry little jig."
	print "You take @n1 by the hand and together dance a merry little jig. Quaint."
	infl me invis skip 2
		infl player invis action cansee "@me dances a very odd little jig on @go own."
		& end
	action cansee DanceTgthr
syntax=none,respond "Get outta here! Are you going to point a gun at my feet and MAKE me dance?"

verb=taste
syntax=noun
	checknear noun,!carrying noun error NotGotNoun,do !burnme
	noun1 dish treatas drink
	!noun1 pan skip 4
		state noun 0 respond "Pointless, it's spotlessly clean! Not even greasy!"
		print "Blech! It's covered in the most revolting stuff! The experience knocks you for six!"
		sub 4 stam me,do !passout
	noun1 cake respond "Either eat it or leave it alone - someone else might want it!"
	noun1 wafer treatas eat
	noun1 biscuit respond "Silly, it'll go all soggy. Just eat it and be done with!"
	noun1 kipper respond "Well, it certainly tastes fishy!"
	respond "It doesn't taste of anything special."
syntax=player=me,respond DontBeDaft
syntax=player,respond "Hmm. Somehow I doubt your intentions have anything to do with 'Taste and see that the Lord is good'..."

verb=sigh,syntax=any,treatas laugh

verb=whistle
syntax=noun=whistle,carrying whistle treatas blow
syntax=none,inroom wlane1 respond "You're not quite a nightingale, are you? But well done for trying."
syntax=any
	carrying whistle syntax whistle none,treatas laugh

verb=sob,syntax=any,treatas laugh

verb=chuckle,syntax=any,treatas laugh

verb=chortle,syntax=any,treatas laugh

verb=clap,syntax=any,treatas laugh

verb=giggle,syntax=any,treatas laugh

verb=growl,syntax=any,treatas laugh

verb=grunt,syntax=any,treatas laugh

verb=burp,syntax=any,treatas laugh

verb=applaud,syntax=any,treatas laugh

verb=cackle,syntax=any,treatas laugh

verb=gulp,syntax=any,treatas laugh

verb=titter,syntax=any,treatas laugh

verb=tut,syntax=any,treatas laugh

verb=fart,syntax=any,treatas laugh

verb=hum,syntax=any,treatas laugh

verb=belche,syntax=any,treatas laugh

verb=crie,syntax=any,treatas laugh

verb=snigger,syntax=none,treatas laugh

verb=sing
syntax=none,inroom wlane1 treatas whistle
syntax=any,treatas laugh

verb=cough,syntax=any,treatas laugh

verb=sneeze,syntax=any,treatas laugh

verb=splutter,syntax=any,treatas laugh

verb=groan,syntax=any,treatas laugh

verb=yelp,syntax=any,treatas laugh

verb=gurgle,syntax=any,treatas laugh

verb=sniff
syntax=noun,treatas smell
syntax=player=me,respond DontBeDaft
syntax=player,error "<Cough>. Perverted or WHAT!!!"
syntax=any,treatas laugh

verb=sniffle,syntax=any,treatas laugh

verb=hic,syntax=any,treatas laugh

verb=cum,syntax=none,respond Vulgar

verb=come,syntax=none,respond "If you're trying to be disgusting, I DO insist you spell it correctly... 'CUM' is probably the word you were looking for, mind you, COME to think of it, you're probably incapable, anyway."

verb=flip
syntax=noun=coin,checknear coin,rand 100 >50 respond Heads,else respond Tails
syntax=any,treatas wave

verb=toss
syntax=noun=coin,treatas flip
syntax=none,respond DontBeVulg,syntax=player,respond DontBeVulg

verb=make
syntax=player text,treatas force
syntax=text player,treatas force
syntax=noun=bed
	checknear noun,!state noun 0 respond "That'd be a waste of time, someone has already made the bed."
	print "You derive a strange joy from tidying the bed up, and in next to no time the job is done!"
	myrank <4 add 2 points me,myrank 4 add 1 point me,state noun 1
	-> action others "@xx has made the bed."
syntax=noun
	carrying noun respond "Why? You're already carrying the @n1!"
	nearto noun respond "Why not just pick the @n1 up!"
	print "That'd be too easy, you'll have to find one..."
syntax=player verb,treatas force

verb=find
syntax=noun
	! infl me blind skip 2
	carrying noun respond "Since you're blind, I'll let you know that you're carrying the @n1."
	ishere noun respond "I wouldn't normally tell, but since you're blind; you'll find the @n1 here."
	carrying noun respond "Dimwit; you're carrying the @n1."
	respond "I'm not your skivvy pal!"
syntax=player
	! infl me blind respond "Do your own dirty work - thats part of the fun!"
	sameroom player respond "I suppose you can't tell, what with being blind, but @n1 is here."

verb=ride
syntax=player,sameroom player respond "You hope!",else error "I can't see @n1 here."
syntax=noun
	checknear noun
	noun1 seesaw respond "It's far too old and rickety, you could do yourself an injury! You could manage to push or tip it from side to side, though."
	noun1 cow respond "It considers itself too important to let you ride it, and dodges all your attempts."
	noun1 dog respond "The dog already has a full load of fleas, so give you that one a miss."
	noun1 cat respond "As you attempt to mount, you ruffle it's fur, and it darts away to preen itself."
	noun1 badger respond "Well, I say LARGE badger, but I'm only talking relative. And besides that, you can't get close enough to it to try."
	noun1 bear respond "No, no, no *NO*. I just can't believe that someone REALLY wants to try and ride on the back of a TEDDY bear!"
	noun1 alien respond 'It replies with a, ahem, "single" gesture, universally recognised to indicate disapproval.'
syntax=noun=wagon
	infl me invis error WagInvis,infl me sinvis error WagInvis
	checknear noun,destroy noun	; destroy: stop it being re-used.
	print RideWag,action cansee GoWagon
	move widet5,action cansee WagPass1
	move widet4,action cansee WagPass1
	move widet3,action cansee WagPass1
	move widet2,action cansee WagPass1
	move widet1,action cansee WagPass1
	move nart2,action cansee WagArr1
	objaction !wagon WagPass2,recover wagon,send wagon wideface
	objaction wagon WagArr2,respond WagStop
syntax=noun,respond "You can look at it, but you can forget riding it."

verb=conduct
syntax=verb none
	!carrying baton respond "No, we are not a lightning conductor, silly."
	respond "You swish the baton around, merrily, but to no avail!"

verb=pray
syntax=verb none
	inroom prayroom setpre me "prayerful",&> respond PrayPray
	inroom confessnl respond PrayConf,inroom home respond PrayHome
	respond JustPray
syntax=verb text,treatas !speech
syntax=verb player text,respond PrayPers
syntax=verb text player,respond PrayPers

verb=confess
syntax=verb none
	inroom prayroom respond ConfPray,inroom confessnl respond Confess
	respond "Confess what, be more specific."
syntax=any,treatas !speech

verb=relax
syntax=none
	fighting me respond "You need your wits about you whilst you're fighting!"
	inroom shute respond "Try as you will, you're heading downward too fast to relax!"
	inroom limbo respond "You're not supposed to get comfortable here, you know..."
	nearto cosy treatas sleep
	lying skip 1,! sitting respond "It's not easy stood up, maybe you should sit down?"
	respond "Bad move... It helps to have your wits about you in this game. Maybe you should go make a cup of tea or send a cheque off to poor, under-paid Oliver Smith?"
syntax=noun
	checknear noun,noun1 bed syntax none none,respond "JUST relax; man."
syntax=player=me,syntax none none
syntax=player,!cansee player respond PlyrNotHere
	print "Well, OK, you suggest to @n1 to relax, but it's not your decision!"
	tell player "@xx suggests that you relax."

verb=wear
syntax=noun,!carrying noun error NotGotNoun,do !burnme
	noun1 tie destroy noun,& respond "It's very old and fragile. Your clumsy attempts to put it round your neck, although successful, result in it disintegrating; completely."
	noun1 coat respond "Humpf! It doesn't fit."
	noun1 cloak print "You can't get it to stay on for more than a moment, so you give up.",& do .uninvis
	error CantVBThat

verb=smoke
syntax=none
	respond "No thanks, it's a disgusting habbit, and if I had lungs I'd treat them with more respect."

verb=touch
syntax=player=me,respond "Don't be so perverse."
syntax=player,treatas hug
syntax=noun
	checknear noun
;	noun1 mobile respond "The @n1 doesn't want to be touched!" (AMUL 907)
	noun1 dog respond NoTchMob,noun1 cow respond NoTchMob
	noun1 cat respond NoTchMob,noun1 fly respond NoTchMob
	noun1 badger respond NoTchMob,noun1 rat respond NoTchMob
	noun1 frog respond NoTchMob,noun1 toad respond NoTchMob
	noun1 ghost respond "Silly, it's a GHOST! You CAN'T touch it!"
	noun1 mud respond "YEUCH! It's thick, goey and slimey!"
	noun1 statue print "It feels, well, leathery.",&> provoke cow 0
	!noun1 circle respond Nothing
; We know it's the circle now:
	!dec noun1 respond Nothing
	!infl me invis do .invis,else print Nothing
	objaction circle CircStop,cancel .endcirc

; ------------------------- Help Text Commands ---------------------------- ;

verb=hints,syntax=any,respond hints

verb=spells,syntax=any,respond spells

verb=levels,syntax=any,respond levels

verb=commands,syntax=any,respond commands

verb=scoring,syntax=any,respond scoring

verb=about,syntax=any,respond about

verb=acting,syntax=any,respond acting

verb=communicate,syntax=any,respond comms

verb=tasks,syntax=any,respond tasks

; -------------------------- Master Commands ------------------------------ ;

verb=.eleyna
syntax=verb
	cure me sinvis,cure me invis
	maketop 99,action cansee "@me has been remapped into a master!"
	cure me sleep,cancel .deepsleep,cure me blind,cure me deaf
	print "You have been remapped into a Master. You awake to find yourself in the Master's Store."
	look

verb=eleyna
syntax=verb
	!inroom depelect error $38
	toprank respond "You're already a Master, so it has no effect."
	print PassOut
	action others "You watch in horror as @me is de-animated, mapped and then removed."
	inflict me blind,inflict me deaf,inflict me sleep,inflict me sinvis
	move store,start .deepsleep 50
	action cansee "The body of @me the @mr appears, slumped on the floor. Blue sparks arc across @go body, which twitches agitatedly."
	start .eleyna 30

verb=set_pre,syntax=any,!toprank error $38
syntax=player text,setpre player noun2,who v,respond "OK, @n1 is now '@n2'."
syntax=any,error SyntVPT

verb=set_post,syntax=any,!toprank error $38
syntax=player text,setpost player noun2,who v,respond "OK, @n1 is now '@n2'."
syntax=any,error SyntVPT

verb=set_arr,syntax=any,!toprank error $38
syntax=player text,setarr player noun2,respond "@n1 arrives as '@n2'."
syntax=any,error SyntVPT

verb=set_dep,syntax=any,!toprank error $38
syntax=player text,setdep player noun2,respond "@n1 departs as '@n2'."
syntax=any,error SyntVPT

verb=announce,syntax=any,!toprank error $38
syntax=text,-> announce everyone text
syntax=text player,print "@n2 sees:^M@n1",-> tell player text
syntax=player text,syntax text player

verb=send,syntax=any,!toprank error $38
;syntax=player verb=sleep,treatas verb=sleep noun1=player noun2=none
syntax=player=me room,respond "Use SuperGo - just type the room ID, eg: tavfrnt."
syntax=player room,respond "Sorry - can't manage that till some future version of AMUL!"
syntax=noun room,respond "Sorry - it's not possible to do that under AMUL 0.9!"

verb=halucinate,syntax=any,!toprank error $38
syntax=text,action everyone text

verb=obgo ?EHCA,syntax=any,!toprank error $38
syntax=noun
	nearto noun error "You're already here! Type 'what' for a list of nearby objects..."
	!exists noun error "The @n1 has been destroyed! You can't do that!"
	print $41
	action cansee $42
	move *noun
	action cansee $43

; ------------------------------ Trailers --------------------------------- ;

verb=.badtrans,syntax=any,objaction transhere badtrans,gstart .badtrans `940

verb=.tingle,syntax=any,objannounce phonedist "For a moment you have a strange tingling sensation, but then it passes...",gstart .tingle `1300

verb=.endpring,syntax=any,state phone 0

verb=.killpring,syntax=any,state phone 1,gstart .endpring 20

verb=.pringing
syntax=any
	!state phone 2 end
	objannounce phone "!! ** RING RING ** !!",objannounce phonering "Ring Ring!"
	gstart .pringing 12

verb=.pring
syntax=any
	state phone 2
	objannounce phone "Suddenly, without warning, the phone starts to ring!"
	objannounce phonering "Somewhere nearby a phone starts to ring..."
	objannounce phonedist "Somewhere in the distance you hear a phone ringing..."
	gstart .pringing 12,gstart .killpring 142

verb=.pring1,syntax=any,gstart .pring `40,gstart .pring1 `1400

verb=mpr,syntax=any,!toprank error $38
syntax=noun=phone
	!state phone 0 error AlreadyDone
	cancel .pring1, gstart .pring1 `1200, gstart .pring  `6,respond "--|> Done!"
syntax=any,error Nothing

verb=.fine3,syntax=any,!state rain 0 objaction rain RainStop,state rain 0

verb=.fine2,syntax=any,!state rain 0 objaction rain RainDisp,& gstart .fine3 `60

verb=.fine,syntax=any,!state rain 0 objaction rain RainEase,& gstart .fine2 `42

verb=.rain3
syntax=any
	!state rain 1 objaction rain RainStart,& gstart .fine `400
	state rain 1

verb=.rain2,syntax=any,!state rain 1 action everyone RainThund,& gstart .rain3 `30

verb=.rain,syntax=any,!state rain 1 objaction rain RainClouds,& gstart .rain2 `26

verb=!raincheck,syntax=any,cancel .rain3,cancel .rain2,cancel .rain,cancel .fine3,cancel .fine2,cancel .fine

verb=!weather,syntax=any,do !raincheck,!state rain 0 gstart .fine `16,else gstart .rain `10

verb=.weather,syntax=any,do !weather,gstart .weather `900

verb=.timelim3,syntax=any,print "*** Online time-limit exceeded! Saving & quitting -- BYE! ***",save,killme

verb=.timelim2,syntax=any,print "*** Last warning! 60 seconds to time limit ***",start .timelim3 60

verb=.timelim1,syntax=any,print "*** WARNING - 3 minutes on-line time remaining ***",& start .timelim2 120

verb=.ptfeel2
syntax=any
	rand 100 >75	print "--|> The ruling powers have decided to award you a bonus 20 points. <|--"
	&> add 20 points me
	stat score me <40 skip 2
	rand 100 <25	print "--|> The ruling powers have decided to deduct 15 points from you, just to be nasty. <|--"
	&> sub 15 points me
	print "The tingling sensation in your ears stops, suddenly, and you feel mentally wet."

verb=.ptfeel1,syntax=any,print "Your ears tingle asthough you're being discussed...",start .ptfeel2 120

verb=!start dream
syntax=any
	print "^MCurrent players: {",who b,print " "
	action outside "@me the @mr has entered The Land."
	!toprank print "*** Maximum on-line time is 50 minutes ***^M",& start .timelim1 2820
	!dec msgcount state msgcount 4,& start .ptfeel1 `10

verb=!boot
syntax=any
	gstart .pring1 `80,gstart .weather `500
	gstart .badtrans `900, gstart .tingle `800
	destroy diamond

; ------------------------ Mobile Command Tables --------------------------- ;

!lapmilk			; Cat drinks the milk
syntax=any
	state dish 0 action others "The cat eyes the dish, hopefully, and meoooww, thirstily."
	else action others "The cat runs to the bowl and greedily laps up all the milk."

!cat
syntax=any
	action others "The cat wags its tail, impatiently."
	action others "The cat rubs itself, lovingly, against your legs."
	announce others "The cat purrs, loudly and lovingly."
	announce others "The cat mews, pitifully."
	!light announceto here "You hear something scratching nearby..."
	ishere dish do !lapmilk			; drink the milk

!dog
syntax=any
	action others "The dog scratches itself."
	action others "For a moment or two the dog looks around, expectantly wagging its tail. Then it gives up, somewhat dissapointed."
	action others "Like a flash, the dog pounces on something on the floor, and then proceeds to stare at the nothing it caught."
	ishere cat announce here "The dog snarls, angrily, at the cat."
	ishere trees action here "The dog sniffs curiously at one of the nearby trees."

!toad
syntax=any
	announce here "REBBIT REBBIT!"
	announce here "The toad splashes about a bit."

!cow
syntax=any
	announce others 'The cow MOOs loudly.'
	& announce outside "In the distance you hear the loud, lonely MOO of an unhappy cow."
	announce others "MOOOOOOOOOOOO."
	action cansee "Quite deliberately, the cow ignores you."
	action cansee "The cow swishes its tail too and fro in an agitated manner."
	ishere fly announce others "The cow belows loudly in annoyance."
	ishere fly action cansee "The cow frustratedly swishes it's tail at the fly."

!alien
syntax=any
	announce cansee 'The alien says "upgv an gbt V rinu blo"!'
	action cansee 'The alien wobbles around a bit and then stands still.'
	announce cansee 'The alien hums, loudly, "Uhznaf znxr n ybiryl zrny... Dum de dum".'

!frog
syntax=any
	action others "The frog hops about a bit, and then settles again."
	action others "The frog rolls its eyes at you."
	announce others "REBBIT, REBBIT."

!ghost
syntax=any
	inroom tavcaretk action others "The ghost appears to be crying. Suddenly it realises it's being watched and pulls itself together."
	!ishere book action others "The ghost seems to be rummaging around, searching through things."
	action others "The ghost tries to show off by floating through a wall or two."
	action others "Trying desperately to scare you, the ghost pulls several faces which only make you want to laugh."
	announce cansee "The ghost wails 'WOOOOOOOOH' at you, and then shudders at it's own voice."
	announce notsee "Something nearby wails a pitifull wail. 'WOOOOOOH' it wails."
	ishere book action others "The spectre tries to pick up the hymn book, but can't manage it."

!badger
syntax=any
	action others "The badger snuffles around, looking for things to eat."

verb=!buffer
syntax=any,end
syntax=any,end
syntax=any,end
syntax=any,end
syntax=any,end

; <End of file>
; -------------------------------------------------
